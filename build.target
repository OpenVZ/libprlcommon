#
# build.target
#
# Copyright (C) 2015 Parallels IP Holdings GmbH
#
# This file is part of Virtuozzo SDK. Virtuozzo SDK is free
# software; you can redistribute it and/or modify it under the
# terms of the GNU Lesser General Public License as published by
# the Free Software Foundation; either version 2.1 of the License,
# or (at your option) any later version.
#
# This library is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# Lesser General Public License for more details.
#
# You should have received a copy of the GNU Lesser General Public
# License along with this library.  If not, see
# <http://www.gnu.org/licenses/>.
#
# Our contact details: Parallels IP Holdings GmbH, Vordergasse 59, 8200
# Schaffhausen, Switzerland; http://www.parallels.com/.
#

NON_SUBDIRS = yes

TEMPLATE = lib
CONFIG += shared
CONFIG -= static
QT -= gui xml

include(Build/Options.pri)
include(common.pri)

TARGET = prlcommon
PROJ_PATH = $$PWD
include(Build/qmake/build_target.pri)

target.path = $${PREFIX}/lib64
INSTALLS += target

unix: QMAKE_LFLAGS += -Wl,-whole-archive

include(common.deps)

unix {
	VERSION = $$cat(Makefile.version)
	VERSIONS = $$split(VERSION, ".")
	VER_MAJ = $$member(VERSIONS,0)
	VER_MIN = $$member(VERSIONS,1)
	VER_PAT = $$member(VERSIONS,2)
}

DEFINES += PRINTABLE_TARGET=common

unix: LIBS += -Wl,-no-whole-archive

INTERFACE_HEADERS = $$system(ls Interfaces/*.h)
headers.files = $${INTERFACE_HEADERS}
headers.path = $${PREFIX}/include/prlcommon/Interfaces
INSTALLS += headers

STD_HEADERS = $$system(ls Libraries/Std/*.h)
stdheaders.files = $${STD_HEADERS}
stdheaders.path = $${PREFIX}/include/prlcommon/Std
INSTALLS += stdheaders
